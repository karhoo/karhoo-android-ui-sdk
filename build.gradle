// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.5.21'
    repositories {
        maven { url "https://jitpack.io" }
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
        mavenCentral()
        mavenLocal()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jaredsburrows:gradle-license-plugin:0.8.1'
        classpath 'com.novoda:bintray-release:0.9.2'
        classpath 'org.jacoco:org.jacoco.core:0.8.7'
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath 'com.google.gms:google-services:4.3.5'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.4.1'
        classpath "com.karumi:shot:5.13.0"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files

        // If you're using the Google Play Services Gradle plugin,
        // you will also need to add this to your build.gradle to
        // avoid a dependency resolution issue:
        components.all {
            allVariants {
                withDependencies { deps ->
                    deps.each { dep ->
                        if (dep.group == 'net.minidev' && dep.name =='json-smart') {
                            dep.version {
                                prefer "2.3"
                            }
                            dep.because "resolving dependencies issue"
                        }
                    }
                }
            }
        }
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt").version("1.17.1")
}

allprojects {
    repositories {
        maven { url "https://jitpack.io" }
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
        mavenCentral()
        mavenLocal()
        maven { url 'https://jitpack.io' }
        maven {
            url "https://cardinalcommerceprod.jfrog.io/artifactory/android"
            credentials {
                username 'braintree_team_sdk'
                password 'AKCp8jQcoDy2hxSWhDAUQKXLDPDx6NYRkqrgFLRc3qDrayg6rrCbJpsKKyMwaykVL8FWusJpp'
            }
        }
    }
    apply from: "$rootDir/detekt.gradle"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    supportLibVersion = "28.0.0"
    archLifecycleVersion = "1.1.1"
}


ext.enableJacoco = { Project project, String variant, String[] projectFileFilter ->
    project.plugins.apply('jacoco')

    final capVariant = variant.capitalize()

    StringBuilder folderSb = new StringBuilder(variant.length() + 1)
    for (int i = 0; i < variant.length(); i++) {
        char c = variant.charAt(i)
        if (Character.isUpperCase(c)) {
            folderSb.append('/')
            folderSb.append(Character.toLowerCase(c))
        } else {
            folderSb.append(c)
        }
    }
    final folder = folderSb.toString()

    project.android.testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }

    project.jacoco {
        toolVersion = '0.8.4'
    }

    project.tasks.withType(Test) {
        jacoco.includeNoLocationClasses = true
        jacoco.excludes = ['jdk.internal.*']
    }

    project.tasks.create(
            name: 'jacocoTestReport',
            type: JacocoReport,
            dependsOn: "test${capVariant}UnitTest"
    ) {
        def buildDir = project.buildDir

        def coverageSourceDirs = [
                "src/main/java",
                "src/main/kotlin"
        ]

        def fileFilter = [
                '**/R.class',
                '**/R$*.class',
                '**/*$ViewInjector*.*',
                '**/*$ViewBinder*.*',
                '**/BuildConfig.*',
                '**/Manifest*.*'
        ]
        fileFilter.addAll(projectFileFilter)

        def javaClasses = fileTree(
                dir: "$buildDir/intermediates/classes/$folder",
                excludes: fileFilter
        )
        def kotlinClasses = fileTree(
                dir: "$buildDir/tmp/kotlin-classes/$variant",
                excludes: fileFilter
        )

        group = "Reporting"
        description = "Generate Jacoco coverage reports for the ${project.name} with the " +
                "$variant variant."
        classDirectories.from files([ javaClasses ], [ kotlinClasses ])
        additionalSourceDirs.from files(coverageSourceDirs)
        sourceDirectories.from files(coverageSourceDirs)
        executionData.from files("${project.buildDir}/jacoco/test${capVariant}UnitTest.exec")
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }
}